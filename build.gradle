plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '8.3.1'
}

wrapper {
    gradleVersion = "8.5"
    distributionType = Wrapper.DistributionType.ALL
}

compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'net.vadamdev.voxel.VoxelGame'
}

group = 'net.vadamdev.voxel'
version = 'Alpha 0.2'

repositories {
    mavenCentral()
}

dependencies {
    //Logging
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "uk.org.lidalia:sysout-over-slf4j:${sysoutOverVersion}"
    runtimeOnly "ch.qos.logback:logback-classic:${logbackVersion}"

    //LWJGL
    implementation platform("org.lwjgl:lwjgl-bom:${lwjglVersion}")
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"

    runtimeOnly "org.lwjgl:lwjgl:${lwjglNatives}"
    runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglNatives}"
    runtimeOnly "org.lwjgl:lwjgl-openal:${lwjglNatives}"
    runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglNatives}"

    //Additional Dependencies
    implementation "org.joml:joml:${jomlVersion}"
    implementation "it.unimi.dsi:fastutil:${fastutilVersion}"
    implementation "io.github.spair:imgui-java-app:${imguiVersion}"
    implementation "com.googlecode.json-simple:json-simple:${jsonSimpleVersion}"

    compileOnly group: "org.jetbrains", name: 'annotations', version: "${jetbrains_annotations_version}"
}
